@DSL DefaultFiniteStrain;
@Behaviour OgdenHyperElasticity;
@Author Thomas Helfer;
@Date 20/12/2016;
@Description{
    Implementation of the Ogden hyperelastic behaviour
}

@MaterialProperty stress K;
K.setGlossaryName("BulkModulus");
@MaterialProperty stress mu;
mu.setEntryName("ParameterMu");
@MaterialProperty real alpha;
alpha.setEntryName("ParameterAlpha");

@LocalVariable StiffnessTensor dS_dC;

@Integrator{
  const auto a   = alpha/2;
  const auto id  = Stensor::Id();
  const auto J   = det(F1);
  const auto C   = computeRightCauchyGreenTensor(F1);
  /* invariants and derivatives */
  const auto C2       = square(C);
  const auto I1       = trace(C);
  const auto I2       = (I1*I1-trace(C2))/2;
  const auto I3       = J*J;
  const auto dI3_dC   = C2-I1*C+I2*id;
  /* volumetric part */
  // Pv = K*(J-1)*(J-1)/2
  const auto dPv_dJ   = K*(J-1);
  const StressStensor Sv = dPv_dJ/J*dI3_dC;
  /* iscochoric part */
  // I1b = J^{-2/3}*I1 = I1/(sqrt[3]{I3})     = I1*iJb
  // I2b = J^{-4/9}*I2 = I2/(sqrt[3]{I3})^{2} = I2*iJb*iJb
  const auto iJb        =  1/cbrt(I3);
  const auto iJb2       =  power<2>(iJb);
  const auto iJb4       =  iJb2*iJb2;
  const auto iJb7       =  iJb4*power<3>(iJb);
  const auto c          = pow(iJb,a-2);
  // derivatives
  const auto diJb_dI3   = -iJb4/3;
  const auto diJb_dC    = diJb_dI3*dI3_dC;
  // eigen values
  tvector<3u,real> vp;
  tmatrix<3u,3u,real> m;
  std::tie(vp,m) = C.computeEigenVectors();
  const tvector<3u,real> pwv  = {pow(vp(0),a-2),pow(vp(1),a-2),pow(vp(2),a-2)};
  const tvector<3u,real> dfv  = {a*vp(0)*pwv(0),a*vp(1)*pwv(1),a*vp(2)*pwv(2)};
  const auto fv    = vp(0)*vp(0)*pwv(0)+vp(1)*vp(1)*pwv(1)+vp(2)*vp(2)*pwv(2);
  const auto df_dC = Stensor::computeIsotropicFunction(dfv,m);
  const StressStensor Si = mu*c*iJb*((fv*diJb_dC+(iJb/a)*df_dC));
  // The Cauchy Stress
  sig = convertSecondPiolaKirchhoffStressToCauchyStress(Sv+Si,F1);
  if(computeTangentOperator_){
    const auto d2I3_dC2   = computeDeterminantSecondDerivative(C);
    /* volumetric part */
    const auto d2Pv_dJ2 = K;
    dS_dC = ((d2Pv_dJ2-dPv_dJ/J)/(2*I3)*(dI3_dC^dI3_dC)+
	     dPv_dJ/J*d2I3_dC2);
    /* isochoric part */
    // second derivative of f
    const tvector<3u,real> d2fv = {a*(a-1)*pwv(0),a*(a-1)*pwv(1),a*(a-1)*pwv(2)};
    const auto d2f_dC2 =
      Stensor::computeIsotropicFunctionDerivative(dfv,d2fv,vp,m,1.e-12);
    // computation of dSi_dC
    const auto d2iJb_dI32 = 4*iJb7/9;
    const auto d2iJb_dC2  =
      d2iJb_dI32*(dI3_dC^dI3_dC)+ diJb_dI3*d2I3_dC2;
    dS_dC += mu*c*((a-1)*fv*(diJb_dC^diJb_dC)+
		   iJb*(fv*d2iJb_dC2+
			((diJb_dC^df_dC)+(df_dC^diJb_dC))+
			iJb/a*d2f_dC2));
  }
}

@TangentOperator<DS_DC>{
  Dt = dS_dC;
}