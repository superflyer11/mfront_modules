@DSL Implicit;
@Behaviour DruckerPragerHyperboloidal;
@Date 08 / 11 / 2024;
@Algorithm NewtonRaphson_NumericalJacobian;
@PerturbationValueForNumericalJacobianComputation 1.e-8;
@ModellingHypotheses{".+"};
@Description{
  Derived a Drucker Prager with parabolodial form with normal plastic flow
  Yield surface defined by:

  Hyperboloidal is fitted inside the cone
  Definition of M_JP in triaxial compression:
  "\["
  "  M_{JP} = \frac{2 \sqrt{3} \sin(\phi')}{3 - \sin(\phi')}"
  "\]"
  Definition of M_JP in triaxial extension:
  "\["
  "  M_{JP} = \frac{2 \sqrt{3} \sin(\phi')}{3 + \sin(\phi')}"
  "\]"
}

@Epsilon 1.e-10;
@Theta 1.;

@Brick StandardElasticity;
@MaterialProperty real phi;
phi.setEntryName("phi");
@MaterialProperty real c;
c.setEntryName("c");
@MaterialProperty real a;
a.setEntryName("proximity");

@LocalVariable real lambda;
@LocalVariable real mu;
@LocalVariable bool bpl;
@LocalVariable real MJP;

@StateVariable strain p;
p.setGlossaryName("EquivalentPlasticStrain");
@AuxiliaryStateVariable StrainStensor epl;
epl.setGlossaryName("PlasticStrain");
@AuxiliaryStateVariable real test;
test.setEntryName("yielding");


@InitLocalVariables {
  lambda = computeLambda(young, nu);
  mu = computeMu(young, nu);
  MJP = 2 * sqrt(3) * sin(phi) / (3 - sin(phi));
}

@Predictor{
  const auto sigel = computeElasticPrediction();
  const auto pel = trace(sigel) / 3;
  const auto qel = sigmaeq(sigel);
  bpl = ( MJP * pel + sqrt(power<2>(a) * power<2>(MJP) + power<2>(qel / sqrt(3))) - MJP * c / tan(phi) > 0);
  test = MJP * pel + sqrt(power<2>(a) * power<2>(MJP) + power<2>(qel / sqrt(3))) - MJP * c / tan(phi);
}

@Integrator {
  const auto id = Stensor::Id();
  const auto seps =  real(1.e-12) * young;
  if (bpl) {
    const auto pr = trace(sig) / 3;
    const auto q = sigmaeq(sig);
    const auto hypterm = sqrt(power<2>(a)*power<2>(MJP) + power<2>(max(q, seps)/sqrt(3)));
    const auto df_dsig   = eval((MJP/3) * id + sqrt(3) * deviator(sig) / (2*hypterm));
    feel += dp * df_dsig; 
    fp =  ( MJP * pr + hypterm - MJP * c / tan(phi))/young;
  }
} // end of @Integrator

@UpdateAuxiliaryStateVariables
{
  // epl += deto-deel;
}
